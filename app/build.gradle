plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def secretPropertiesFile = rootProject.file("secrets.properties")
def secretProperties = new Properties()
secretProperties.load(new FileInputStream(secretPropertiesFile))

android {
    compileSdk 31
    buildFeatures {
        viewBinding true
    }
    defaultConfig {
        applicationId "com.carlosblaya.marveldemo"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    // Specifies one flavor dimension.
    flavorDimensions "server"
    productFlavors {
        dev {
            //Change these values for DEV
            buildConfigField "String", "MARVEL_API_PUBLIC_KEY", "\"${secretProperties.getProperty('app.marvel_public_api_key')}\""
            buildConfigField "String", "MARVEL_API_PRIVATE_KEY", "\"${secretProperties.getProperty('app.marvel_private_api_key')}\""
            buildConfigField "String", "BASE_URL_SERVICE", "\"https://gateway.marvel.com/\""
            dimension "server"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
        pro {
            //Change these values for PRO
            buildConfigField "String", "MARVEL_API_PUBLIC_KEY", "\"${secretProperties.getProperty('app.marvel_public_api_key')}\""
            buildConfigField "String", "MARVEL_API_PRIVATE_KEY", "\"${secretProperties.getProperty('app.marvel_private_api_key')}\""
            buildConfigField "String", "BASE_URL_SERVICE", "\"https://gateway.marvel.com/\""
            dimension "server"
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
        unitTests.returnDefaultValues = false
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    //JETPACK
    def lifecycle_version = "2.3.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.1"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    //Hilt
    implementation "com.google.dagger:hilt-android:2.40.1"
    kapt "com.google.dagger:hilt-android-compiler:2.40.1"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.13.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.0'

    // Activity-KTX
    implementation 'androidx.activity:activity-ktx:1.5.0-alpha03'

    // Fragment-KTX
    implementation 'androidx.fragment:fragment-ktx:1.4.1'

    //Lottie
    implementation 'com.airbnb.android:lottie:3.4.0'

    //Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Room
    implementation "androidx.room:room-runtime:2.4.2"
    implementation "androidx.room:room-ktx:2.4.2"
    kapt "androidx.room:room-compiler:2.4.2"

    // Paging
    implementation 'androidx.paging:paging-runtime-ktx:3.1.0'

    //Testing
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2'

    testImplementation "org.mockito:mockito-inline:3.1.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.2.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.mockito:mockito-core:3.9.0'

    testImplementation "org.json:json:20190722"
    testImplementation "io.mockk:mockk:1.12.0"

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.0")
    testImplementation 'junit:junit:4.13.2'

    testImplementation "app.cash.turbine:turbine:0.2.1"



}